; Bacillus_c64
; Copyright (C) 2016-2019  Thorsten Jordan.
; 
; This program is free software; you can redistribute it and/or modify
; it under the terms of the GNU General Public License as published by
; the Free Software Foundation; either version 2 of the License, or
; (at your option) any later version.
; 
; This program is distributed in the hope that it will be useful,
; but WITHOUT ANY WARRANTY; without even the implied warranty of
; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
; GNU General Public License for more details.
; 
; You should have received a copy of the GNU General Public License
; along with this program; if not, write to the Free Software
; Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA

; ***************************************************************************************
;
; all code related to 2D scrolling (8 directions)
;
; ***************************************************************************************

; Two dimensional scrolling on C64 is implemented with these features:
; - Hardware scrolling works for up to 7 pixels in all directions
; - Then we need to copy the screen and color ram by one char to any direction
; - Double buffering can and should be used for screen data
; - Color ram must be copied in-place and synchronized with raster
; - It takes about half the raster time of a frame to copy the whole screen
;   So we can scroll a char every two frames (need screen and color ram copy and spare time)
; - This means scroll speed is 1, 2 or 4 pixels per frame. 1 is too slow, mostly 2 or 4.
; - Scrolling is always done from fix position in char to next fix position, so always
;   multiple frames at once!
; - Neutral position is with hardware register position in center of char, so we have time
;   to prepare next position
; - Thus the scroll directions must defined by char offsets, hence 8 directions possible!
;
; Note that we could also keep two backbuffers for color that we can prepare in advance,
; but that doesn't make the code faster or easier.
;
; The code related to scrolling is rather simple then:
; We need to memorize if we are scrolling and in what direction.
; Then we need to store the scrolling phase and a global position that is updated and
; can be requested by the level data / tile renderer.
; From outside the next scroll direction is given and also scroll speed.
; Beside managing that data and the hardware registers the code needs to copy screen
; and color ram data at the right time.
; When moving the color ram data during display we have to check where the raster line is
; and copy either the upper or lower part of the color ram accordingly.
; Copying:
; Fastest copying would be a direct LDA/STA pair with maximum unrolling but this takes too
; much ram for code. We need 3 variants: copy screen 0 to screen 1 and vice versa and copying
; in the color ram. And we need the code for all 8 directions.
; So we use a loop that iterates over columns and is unrolled for lines. This is still very
; fast (9 cycles per byte where 8 cycles at least needed for fastest direct copy), but is
; acceptable for code space. It takes 21 LDA,x/STA,x pairs plus 4-5 byte overhead for each
; copy routine and we need 3*8 instances: (21*6+5)*3*8 = ~3kb, a lot of code.
; We can cut that down by modifying the address highbytes in the copy code and using only
; the 8 instances for 8 directions. Modification is 42 STA and 2 LDA, because source
; and destination highbytes must be set, so we have:
; (20*6+5+42*3+2*3)*8 = ~2kb. Preparing the addresses takes 42*4+4*2=176 cycles about 3 RL
; per frame, which is ok. So we can trade 1kb of code for 3 RL, but directly having all
; instances unrolled can be easier. Switching screen addresses would be easier with the
; preparation code though. We can load source/destination addresses by scroll offset
; and don't need to handle twice the jump tables.
; We could even go further and modify also the lower bytes of read/write addresses
; but this takes too much time. As we use only the X register for offset we could also
; use Y (at the price of one INY/DEY each column) and we can handly +/- one line offsets
; with the same code. But scrolling updwards needs special code where we store one line
; temporarily and scrolling right needs increment of offset where left/up/down can use
; slightly faster decrement (zero check is faster than CPX/CPY).
; Using X/Y for line offsets will make the code even shorter but much more complex in
; development. This would save maybe another 0.5-1kb of memory and should be done only
; if the memory is short. For now it is best to implement the full unrolled version.
; The code to manage scrolling should be called when raster reaches bottom of screen,
; however when sprite muxing is done there and taking away most of the time until screen
; start this is too late. We need to start colorram copy of the upper half when raster
; has done drawing the upper half. So we need to call it after 10-11 lines and at end
; of visible area (21 lines, before score panel).
; Then we can copy the parts of the screen that were just rendered.
;
; Note on how the hardware register control works:
; Register position 0 means we see all of the chars, so screen and color data must
; be correct and match. Other values mean that the first row/col is partly invisible
; and the last row/col is partly visible.
; The color ram must be correct when hardware register reaches 0. We set the hardware
; register at the bottom of the screen (write to some memory so it is set correctly
; after score panel - this means we need an irq sometimes after the score panel).
; So when the register will be set to 0 at bottom we need to prepare parts of the
; screen or already have them prepared. Color ram upper half (lines 0-9) must have
; been already copied when they were drawn, so we need to copy the lower part
; (lines 10-20) then. This is done when the current register has the value before 0!
; And one frame before we need to prepare the screen.
; This means with scroll speed of 4 pixels there are some pitfalls: since we need
; to prepare the screen when register will be 4 as next and neutral position is 4,
; we need to delay scroll start one frame when not scrolling. And we need to prepare
; next screen after we reached neutral position 4 if scrolling continues.
; This is a bit more complex. With scroll speed of 2 pixels this is simpler.
; Hardware registers go 4->6->0->2->4. So we have more frames to decide what to do
; next. Note scrolling in opposite direction is 4->2->0->6->4 so 2/6 in the tables
; below must be adjusted to situation.
;
; 4 pixel scroll speed
; Hardware scroll    Where         Do what
; 4                  mid screen    copy upper color part when scrolling is active
; 4                  end screen    copy lower color part when scrolling is active
; 0                  mid screen    copy upper screen part to backbuffer if scrolling is active
; 0                  end screen    copy lower screen part to backbuffer if scrolling is active
;
; 2 pixel scroll speed
; Hardware scroll    Where         Do what
; 4                  mid screen    copy upper screen part to backbuffer if scrolling is active
; 4                  end screen    copy lower screen part to backbuffer if scrolling is active
; 6                  mid screen    copy upper color part when scrolling is active
; 6                  end screen    copy lower color part when scrolling is active
; 0                  mid screen    nothing
; 0                  end screen    nothing
; 2                  mid screen    nothing
; 2                  end screen    nothing
;
; With 4 pixel/frame speed it means that on scroll start we have to delay it one frame anyway.
; And on scroll end we either need to continue preparing the next screen in scroll direction
; or do nothing, depending on scroll speed.
; Maybe we should give the scroll code a screen position to scroll to, so it can decide
; on its own when scrolling starts or stops, and what direction to use.
; It could even adjust the scroll speed depending on difference.
; But that is also not trivial.
;
; Note that screen has 38 visible chars in width and the 39th partially. So we need
; to have only 39 chars prepared. So we can ignore the last column!
;
; Note that all right scrolling directions need forward copy for color ram,
; and all upward scrolling need one temporary line to save data to (slower)
; but we can't scroll the whole color ram at once, so we need to store line 9
; to a buffer and then shift 0-8 one down. Later shift 10-20 down and copy
; buffer to line 10 then.
; So use directions 0-7 for left/upwards, upwards, right/upwards, right and so on.
; Directions 0-2 need temporary buffer, 2-4 need forward move. Copy from/to buffer
; can always be done backwards (DEX/DEY). Rather use 1-8 to signal 0 as no scroll.
;
; Sprite muxer irq that is called for score panel could set up the score panel,
; then do sprite mux data, reset screen data and then ack irq and then call this scrolling code...

; fixme: is_scroll_active remove that from main program. same for screen_pos_x.

; fixme only to the plain scrolling here and call fill_new_first_row etc. functions here
; that are implemented in tile rendering!
; do tile rendering in other file!

; These macros used below copy screen parts line based. There are several advantages: when scrolling this only copies
; bytes that are kept, remaining columns can be filled up easily. It is very easy to write and to use, better than
; approaches that use some power of two as loop counter. Code is rather short (~100 bytes for half screen).
; And it uses partial loop unrolling, taking ((4+5)*12+7)*39 = 4485 cycles per half screen (71.2 raster lines),
; Which is pretty fast (9.6 cycles/byte, optimum with maximal unrolling only 8 cycles/byte!).
; Final advantage: there are no overlaps and no bytes to save/restore.
; And the same macro can be used for color ram shifting as well!
; It takes ~80 raster lines (with bad lines ~58 cycles per rasterline medium, so seems ok)
; Full screen is 312 raster lines, so it takes ~ half raster time to copy, seems fair.

; Note! For parallax scrolling we need to counter-rotate some chars and we need to do that
; for all 8 directions! Data is rotated over several characters. This can be done but
; is another larger part of code, needs diligence to code it.

; color data generation:
; either copy tile data to temporary buffer when creating new row/col data and later
; copy that data to screen or render color information of tiles to color ram.
; Note that the offset would be different then as char data was generated with different
; offset as it was a frame before. But this happens only when rendering tiles directly
; to screen what was used when some boni were collected. If we use sprites for boni
; we can avoid that complexity!

; fixme debugging
; it crashes in some dir! up left.
; we need data generator.
; screen data transfer seems broken, start screen never really disappears.
; there is some logic error with source/dest, but basically it seems to work.
; color ram scroll left is ok, scroll right not.
; at least there is already action and not too far away...

!if TESTING {
TEMP0 = $2
VICBGCOL = $D020
VICFGCOL = $D021
VICCTRL2 = $D016
VICCTRL1 = $D011
VICRASTER = $D012
COLORRAM = $D800
VICBANK = 0
VICMEMCTRL = $D018
VICCHARSETBLOCK = 2	; block 2 has offset 4k
SCREEN0POSITION = 1
SCREEN1POSITION = 15
SCREEN0 = VICBANK*$4000+SCREEN0POSITION*$0400
SCREEN1 = VICBANK*$4000+SCREEN1POSITION*$0400
CIA1_DATAPORT_A = $DC00

!to "test/scrolling2d.prg", cbm

!src "src/macros.a"

*= $0801	; Program start at 2049

; Basic start code 1979 SYS2066:REM TJ
!byte $10,$08,$BB,$07,$9E,$32,$30,$36,$36,$3A,$8F,$20,$54,$4A,$00,$00,$00

main
	; set up multicolor mode and scroll situation initially.
	jsr scrolling_init
	; wait for raster to be at mid of screen
--	lda #130
-	cmp VICRASTER
	bcs -
	; call midcode
!if DEBUG_TIMING { inc VICBGCOL }
	jsr scrolling_midscreen
!if DEBUG_TIMING { dec VICBGCOL }
	; wait for raster to be at end of screen
	lda #218
-	cmp VICRASTER
	bcs -
	; call endcode
!if DEBUG_TIMING { inc VICBGCOL }
	jsr scrolling_endscreen
!if DEBUG_TIMING { dec VICBGCOL }
	; read joystick control for scroll testing, joystick bits to direction!
	lda CIA1_DATAPORT_A	; last four bits are up, down, left, right, fire
	and #$0F
	eor #$0F		; negate bits
	tax
	lda joy_scrolldir,x
	;sta VICFGCOL ;fixme for control testing
	sta scroll_active
	; repeat, but wait first until raster is again near start of screen
-	lda VICRASTER
	bmi -
	jmp --

joy_scrolldir
!byte 0, 2, 6, 0,  8, 1, 7, 0,  4, 3, 5, 0,  0, 0, 0, 0

; for testing
scroll_generate_first_row
	lda scroll_screen_msb_dst
	sta sgfr-1
	lda screen_pos_x+1
	clc
	adc screen_pos_y+1
	tay
	ldx #39
-	tya
	sta scroll_color_newdata_tmpbuf_row-1,x
	sta SCREEN0-1,x
sgfr
	iny
	dex
	bne -
	rts

scroll_generate_last_row
	lda scroll_screen_msb_dst
	sta sglr-1
	lda screen_pos_x+1
	clc
	adc screen_pos_y+1
	adc #$33
	tay
	ldx #39
-	tya
	sta scroll_color_newdata_tmpbuf_row-1,x
	sta SCREEN0-1,x
sglr
	iny
	dex
	bne -
	rts

scroll_generate_first_col
;fixme iterations columns not easy
	rts

scroll_generate_last_col
	rts

}


screen_pos_x
!word 0

screen_pos_y
!word 0

scroll_active
!byte 0

scroll_direction
!byte 0

scroll_phase
!byte 0

scroll_srcdst_flag
!byte 0

scroll_vicmemctrl
!byte 0

scroll_screen_msb_src
!byte 0
scroll_screen_msb_dst
!byte 0

scroll_color_tmpbuf
!for COUNT, 0, 38 {	; we need 39 bytes at most
!byte 0
}

scroll_color_newdata_tmpbuf_row
!for COUNT, 0, 38 {	; we need 39 bytes at most
!byte 0
}

scroll_color_newdata_tmpbuf_col
!for COUNT, 0, 24 {	; we need 25 bytes at most
!byte 0
}

; ----------- tables with data

scroll_delta_x	;fixme use one table of 10 bytes with offset 2!
!byte $E0, $00, $20, $20, $20, $00, $E0, $E0

scroll_delta_y
!byte $E0, $E0, $E0, $00, $20, $20, $20, $00

; Note that the tables alone take 64byte plus alignment.
!align 15,0
scroll_jmp_table_mid_screen
!word scroll_left_up_midscreen
!word scroll_up_midscreen
!word scroll_right_up_midscreen
!word scroll_right_midscreen
!word scroll_right_down_midscreen
!word scroll_down_midscreen
!word scroll_left_down_midscreen
!word scroll_left_midscreen

scroll_jmp_table_end_screen
!word scroll_left_up_endscreen
!word scroll_up_endscreen
!word scroll_right_up_endscreen
!word scroll_right_endscreen
!word scroll_right_down_endscreen
!word scroll_down_endscreen
!word scroll_left_down_endscreen
!word scroll_left_endscreen

scroll_jmp_table_mid_color
!word scroll_color_left_up_midscreen
!word scroll_color_up_midscreen
!word scroll_color_right_up_midscreen
!word scroll_color_right_midscreen
!word scroll_color_right_down_midscreen
!word scroll_color_down_midscreen
!word scroll_color_left_down_midscreen
!word scroll_color_left_midscreen

scroll_jmp_table_end_color
!word scroll_color_left_up_endscreen
!word scroll_color_up_endscreen
!word scroll_color_right_up_endscreen
!word scroll_color_right_endscreen
!word scroll_color_right_down_endscreen
!word scroll_color_down_endscreen
!word scroll_color_left_down_endscreen
!word scroll_color_left_endscreen

dummy
	jmp * ;fixme doesnt arrive here!
	rts	;fixme test do nothing on color!

dummy2
	jmp * ;fixme doesnt arrive here!
	rts	;fixme test do nothing on color!

;fixme same values for both control registers! if that works, spare one copy!
scroll_vicctrl1
!byte $17,$16,$15,$14,$13,$12,$11,$10	; hibit irq off, extended color mode off, bitmap off, screen on

scroll_vicctrl2
!byte $17,$16,$15,$14,$13,$12,$11,$10	; multicolor bit enabled!

; copy data from temporary buffers to color ram.
; note that we can use index register for horizontal copy,
; but that adds 39*(1+2+3)=234 cycles (4 RL) to the copy cost (8*39=312 | 5.3 RL)
; and we would save 38*6-3=225 bytes of ram. Rather use the ram.
; this also means all the color copy eats another 1kb of ram!
scroll_copy_color_first_row
!for COUNT, 0, 38 {
	lda scroll_color_newdata_tmpbuf_row+COUNT
	sta COLORRAM+COUNT
}
	rts

scroll_copy_color_last_row
!for COUNT, 0, 38 {
	lda scroll_color_newdata_tmpbuf_row+COUNT
	sta COLORRAM+20*40+COUNT
}
	rts

scroll_copy_color_first_col
!for COUNT, 0, 20 {
	lda scroll_color_newdata_tmpbuf_col+COUNT
	sta COLORRAM+COUNT*40
}
	rts

scroll_copy_color_last_col
!for COUNT, 0, 20 {
	lda scroll_color_newdata_tmpbuf_col+COUNT
	sta COLORRAM+COUNT*40+38
}
	rts

; Use a macro that is called with source/dest offset and number of rows and start row.

!macro screen_part_copy .srcoff, .dstoff, .numcols, .numrows, .rowoff {
!for ROW, 0, .numrows-1 {
	stx .copycode+ROW*6+2
	sty .copycode+ROW*6+5
}
	ldy #.numcols
.copycode
!for ROW, .rowoff, .rowoff+.numrows-1 {
	lda SCREEN0+ROW*40-1+.srcoff,y
	sta SCREEN1+ROW*40-1+.dstoff,y
}
	dey
	bne .copycode
}

!macro color_part_copy .srcoff, .dstoff, .numcols, .numrows, .rowoff, .right, .upward {
!if .right {
	ldy #1	; reverse has offset 1 so we need to use that on forward too
} else {
	ldy #.numcols
}
.copycode
!if .upward {
!set .extraoffset = 0
!if .rowoff = 0 {
!set .extraoffset = 1
	lda COLORRAM+(.numrows-1)*40-1,y
	sta scroll_color_tmpbuf-1,y
}
!for ROW, .rowoff+.numrows-1-.extraoffset, .rowoff {	; copy upwards
	lda COLORRAM+ROW*40-1+.srcoff,y
	sta COLORRAM+ROW*40-1+.dstoff,y
}
!if .rowoff > 0 {
	lda scroll_color_tmpbuf-1,y
	sta COLORRAM+.rowoff*40-1,y
}
} else {
!for ROW, .rowoff, .rowoff+.numrows-1 {	; copy downwards
	lda COLORRAM+ROW*40-1+.srcoff,y
	sta COLORRAM+ROW*40-1+.dstoff,y
}
}
!if .right {
	iny
	cpy #(.numrows+1)
} else {
	dey
}
	bne .copycode
}

scroll_left_up_midscreen
	+screen_part_copy 0, 41, 38, 10, 0
	rts

scroll_left_up_endscreen
	+screen_part_copy 0, 41, 38, 10, 10
	jsr scroll_generate_first_row
	jmp scroll_generate_first_col

scroll_up_midscreen
	+screen_part_copy 0, 40, 39, 10, 0
	rts

scroll_up_endscreen
	+screen_part_copy 0, 40, 39, 10, 10
	jmp scroll_generate_first_row

scroll_right_up_midscreen
	+screen_part_copy 1, 40, 38, 10, 0
	rts

scroll_right_up_endscreen
	+screen_part_copy 1, 40, 38, 10, 10
	jsr scroll_generate_first_row
	jmp scroll_generate_last_col

scroll_right_midscreen
	+screen_part_copy 1, 0, 38, 10, 0
	rts

scroll_right_endscreen
	+screen_part_copy 1, 0, 38, 11, 10
	jmp scroll_generate_last_col

scroll_right_down_midscreen
	+screen_part_copy 41, 0, 38, 10, 0
	rts

scroll_right_down_endscreen
	+screen_part_copy 41, 0, 38, 10, 10
	jsr scroll_generate_last_row
	jmp scroll_generate_last_col

scroll_down_midscreen
	+screen_part_copy 40, 0, 39, 10, 0
	rts

scroll_down_endscreen
	+screen_part_copy 40, 0, 39, 10, 10
	jmp scroll_generate_last_row

scroll_left_down_midscreen
	+screen_part_copy 40, 1, 38, 10, 0
	rts

scroll_left_down_endscreen
	+screen_part_copy 40, 1, 38, 10, 10
	jsr scroll_generate_last_row
	jmp scroll_generate_first_col

scroll_left_midscreen
	+screen_part_copy 0, 1, 38, 10, 0
	rts

scroll_left_endscreen
	+screen_part_copy 0, 1, 38, 11, 10
	jmp scroll_generate_first_col

scroll_color_left_up_midscreen
	+color_part_copy 0, 41, 38, 10, 0, 0, 1
	rts

scroll_color_left_up_endscreen
	+color_part_copy 0, 41, 38, 10, 10, 0, 1
	jsr scroll_copy_color_first_row
	jmp scroll_copy_color_first_col

scroll_color_up_midscreen
	+color_part_copy 0, 40, 39, 10, 0, 0, 1
	rts

scroll_color_up_endscreen
	+color_part_copy 0, 40, 39, 10, 10, 0, 1
	jmp scroll_copy_color_first_row

scroll_color_right_up_midscreen
	+color_part_copy 1, 40, 38, 10, 0, 1, 1
	rts

scroll_color_right_up_endscreen
	+color_part_copy 1, 40, 38, 10, 10, 1, 1
	jsr scroll_copy_color_first_row
	jmp scroll_copy_color_last_col

scroll_color_right_midscreen
	+color_part_copy 1, 0, 38, 10, 0, 1, 0
	rts

scroll_color_right_endscreen
	+color_part_copy 1, 0, 38, 11, 10, 1, 0
	jmp scroll_copy_color_last_col

scroll_color_right_down_midscreen
	+color_part_copy 41, 0, 38, 10, 0, 1, 0
	rts

scroll_color_right_down_endscreen
	+color_part_copy 41, 0, 38, 10, 10, 1, 0
	jsr scroll_copy_color_last_row
	jmp scroll_copy_color_last_col

scroll_color_down_midscreen
	+color_part_copy 40, 0, 39, 10, 0, 0, 0
	rts

scroll_color_down_endscreen
	+color_part_copy 40, 0, 39, 10, 10, 0, 0
	jmp scroll_copy_color_last_row

scroll_color_left_down_midscreen
	+color_part_copy 40, 1, 38, 10, 0, 0, 0
	rts

scroll_color_left_down_endscreen
	+color_part_copy 40, 1, 38, 10, 10, 0, 0
	jsr scroll_copy_color_last_row
	jmp scroll_copy_color_first_col

scroll_color_left_midscreen
	+color_part_copy 0, 1, 38, 10, 0, 0, 0
	rts

scroll_color_left_endscreen
	+color_part_copy 0, 1, 38, 11, 10, 0, 0
	jmp scroll_copy_color_first_col


; fixme strip the parallax and tile render code out and use the existing code
; for left/right as source how to handle the scrolling, register programming and so on

!macro scroll_jmp_table_call .tableadr {	; takes 16 cycles.
	asl
	clc
	adc #((<.tableadr) - 2)
	sta .jmpadr+1
.jmpadr
	jmp (.tableadr)
}

scrolling_init
	; set up VIC control register and screen position to half char shift
	lda scroll_vicctrl1+4
	sta VICCTRL1
	lda scroll_vicctrl2+4
	sta VICCTRL2
	lda screen_pos_x
	ora #$40
	sta screen_pos_x
	lda screen_pos_y
	ora #$40
	sta screen_pos_y
	; clear all helper variables
	lda #0
	sta scroll_active
	sta scroll_direction
	sta scroll_phase
	sta scroll_srcdst_flag
	+set8im VICMEMCTRL, SCREEN0POSITION*16+VICCHARSETBLOCK*2
	rts

scrolling_midscreen
	; in certain scroll phases we need to call code to copy parts of screen
	lda scroll_active	; either scrolling is just activated or running
	ora scroll_direction
	bne +
	rts
+	; check phase
	ldx scroll_phase
	bne +++
	; phase is zero (offset 4), needs screen copy preparation/upper half
	; Toggle flag for source/destination screen
	sta TEMP0
	lda scroll_srcdst_flag
	eor #$80
	sta scroll_srcdst_flag
	bmi +
	lda #SCREEN0POSITION*16+VICCHARSETBLOCK*2
	ldx >SCREEN1
	ldy >SCREEN0
	bne ++	; is never zero, this is a jmp
+	lda #SCREEN1POSITION*16+VICCHARSETBLOCK*2
	ldx >SCREEN0
	ldy >SCREEN1
++	sta scroll_vicmemctrl
	stx scroll_screen_msb_src
	sty scroll_screen_msb_dst
	lda TEMP0
	+scroll_jmp_table_call scroll_jmp_table_mid_screen
+++	cpx #1
	beq +
	rts
+	; phase is one (offset 6/2) needs color copy for upper half
	+scroll_jmp_table_call scroll_jmp_table_mid_color

scrolling_endscreen
	lda scroll_active
	ora scroll_direction
	bne +
	rts
+	; transfer scroll direction to y and adjust screen_pos and program hardware register
	tay
	lda scroll_delta_x-1,y	; y register is direction 1-8
	bpl +
	dec screen_pos_x+1
+	clc
	adc screen_pos_x
	sta screen_pos_x
	bcc +
	inc screen_pos_x+1
+	; a has LSB of screen_pos_x where bits 4-6 are hardware register
	lsr
	lsr
	lsr
	lsr
	and #$07
	tax
	lda scroll_vicctrl2,x
	sta VICCTRL2
	lda scroll_delta_y-1,y	; y register is direction 1-8
	bpl +
	dec screen_pos_y+1
+	clc
	adc screen_pos_y
	sta screen_pos_y
	bcc +
	inc screen_pos_y+1
+	; a has LSB of screen_pos_y where bits 4-6 are hardware register
	lsr
	lsr
	lsr
	lsr
	and #$07
	tax
	lda scroll_vicctrl1,x
	sta VICCTRL1
	; increase scroll phase and check for copy operations
	ldx scroll_phase
	inx
	txa
	and #$03
	sta scroll_phase
	cmp #1
	bne +
	; when phase is now 1 do screen copy
	tya	; direction to a for jump table
	ldx scroll_screen_msb_src
	ldy scroll_screen_msb_dst
	+scroll_jmp_table_call scroll_jmp_table_end_screen
+	cmp #2
	bne +
	; when phase is now 2 do colorram copy and switch screens
	lda scroll_vicmemctrl
	sta VICMEMCTRL
	tya	; direction to a for jump table
	+scroll_jmp_table_call scroll_jmp_table_end_color
+	cmp #0
	bne +
	; next phase would be 0 - check for stop scroll - just copy active to direction!
	lda scroll_active
	sta scroll_direction	
+	rts

