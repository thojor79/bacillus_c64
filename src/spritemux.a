; Bacillus_c64
; Copyright (C) 2016-2017  Thorsten Jordan.
; 
; This program is free software; you can redistribute it and/or modify
; it under the terms of the GNU General Public License as published by
; the Free Software Foundation; either version 2 of the License, or
; (at your option) any later version.
; 
; This program is distributed in the hope that it will be useful,
; but WITHOUT ANY WARRANTY; without even the implied warranty of
; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
; GNU General Public License for more details.
; 
; You should have received a copy of the GNU General Public License
; along with this program; if not, write to the Free Software
; Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA

; ***************************************************************************************
;
;    sprite multiplexer V2.0
;
; ***************************************************************************************

SCOREPANEL_VICMEMCTRL = $14	;fixme define from user! 
SCOREPANEL_VICCTRL1 = $18
SCOREPANEL_VICCTRL2 = $08

;define this from outside
!ifndef NR_OF_VIRTUAL_SPRITES {
NR_OF_VIRTUAL_SPRITES = 24
}
SPRITE_Y_DISTANCE = 21		; This is just the sprite height.
UNROLL_FULL_IRQ = 0
STARTFRAME_RASTERLINE = 30
SPRITE_PREPARE_RASTERLINE = 214; 250	; IRQ is triggered 1 line before and then stable line with busy waiting
MIN_SPRITE_Y = 34 ; 30 is lowest possible position, with 24 line mode (scrolling) this is 34.
MAX_SPRITE_Y = SPRITE_PREPARE_RASTERLINE-1
SPRITE_PREPARE_IRQ_RASTERLINE = SPRITE_PREPARE_RASTERLINE - 2

; A sprite multiplexer uses the eight hardware sprites that are reprogrammed with raster IRQs to display
; a larger number of virtual sprites.
; For that it is essential to have very low overhead in the IRQ code so the raster IRQs can be called
; often and quickly reprogram the sprite registers.
; The data for the IRQs is prepared when no sprites are visible, at bottom of the screen, this is
; the preparation phase.
; For every sprite at least four registers have to be set on the IRQ: x, y, pointer, color and we need
; to set the register for the 9th bits of the x coordinates.
; The fastest way to set registers is a direct LDA #immediate, STA $D0XX pair with 6 cycles per register
; set. An older version of this muxer did it that way but this is only possible by manipulating the IRQ
; code every frame which costs much preparation time.
; By reading the values from zeropage or normal memory this takes 7 or 8 cycles per register, which is
; still fast enough for IRQs but makes the preparation code faster.
; So this version of the sprite multiplexer (muxer) uses this approach with still low IRQ overhead.
; See below for more IRQ details.
; Here are more details on how it works:
; The preparation stage copies the virtual sprite data and sorts the sprites by their Y coordinate.
; Sprites that are invisible or would become the 9th hardware sprite on a row are skipped.
; The first eight sprites are displayed directly. More virtual sprites need raster IRQs
; to be fired early enough before their rasterline has come to reprogram the sprite registers.
; IRQs can be triggered as soon as a hardware sprite has finished display or shortly before it has to be displayed.
; This code uses the latter method and also computes in the preparation phase which other hardware sprites could
; be reprogrammed in the same IRQ because their display has also been finished.
; The preparation keeps the sorted Y values and sorts them again with insertion sort in the next frame.
; Normally Y coordinates don't change that often, so the sorting code hasn't much to do in most frames.
; This is the fastest solution for sorting, but it still takes many rasterlines.
; We need to sort indices to the virtual sprites instead of just Y coordinates, so the sorting algorithm needs
; double indirection. A faster solution to just sort the Y coordinates directly isn't useful.
; The preparation time should be as short as possible which is achieved here with one extra
; computation. If more than one sprite is displayed in a raster IRQ we have to trigger that IRQ earlier. Older
; versions or other muxers simply compute how many sprites need to be programmed and decrease the raster line
; for trigger accordingly. But it can be done better: for every sprite take its Y coordinate and compute how many
; sprites are programmed before that in the same IRQ and decrease the raster line according to that number,
; then take the total minimum of all these numbers. If all displayed sprites would have the same Y coordinate
; the result is the same as with other muxers, but if the IRQ is triggered with enough distance to the other
; sprites in the IRQ we can trigger it shorter before the first. This allows for tighter sprite formations
; on the screen without visual errors. This extra computation step takes ~6 raster lines in preparation
; stage.
; Now for the overhead in the IRQ. By using the lda memory, sta $D0XX approach we can use an IRQ that only
; modifies the accumulator, so we don't need so save X/Y registers. The IRQ code is unrolled for every virtual
; sprite above the first eight. IRQ entry can be at any virtual sprite. As first the xhigh register is set for
; all sprites, then the four registers are set for each individual sprite. If the highest bit of the color is set
; the IRQ ends. If not the next unrolled virtual sprite code block is run.
; Now there are two options: full unrolling vs. partial unrolling.
; With partial unrolling it takes 38 bytes for the IRQ code per virtual sprite. When the IRQ ends we do a JSR to a
; routine that uses the calling address on the stack to start the next IRQ there.
; With full unrolling it takes 83 bytes for the IRQ code per virtual sprite. When the IRQ ends the address to
; call the next IRQ is just set by every unrolled code block. Interrupt exit code is contained in every block.
; This saves 20 cycles on exit of every IRQ and 14 for the last IRQ. Even less overhead with the cost of
; much memory. Normally this extra saving of cycles isn't worth the additional used RAM.
; So as memory usage for the IRQ code this gives in bytes:
; Nr. virt. Sprites   Fully unrolled   Partially unrolled
; 12                   296              215
; 16                   628              367
; 20                   960              519
; 24                  1292              671
; 28                  1624              823
; 32                  1956             1127
; Note that sprite data doesn't need to be in zeropage, but IRQ is faster that way and we have enough space
; in the zeropage.
; Note that at the end of an IRQ we have to check if we are late for the next IRQ and if so, we must do
; busy waiting in the IRQ. With tight sprite formations this eats away much time, but it must be done to
; avoid visual glitches.
; 
; For debugging the background color is modified in preparation phase and also for displaying.
; Light grey is IRQ code time and black is busy waiting time.

; Some notes for incorporating the muxer in a game with scrolling:
; The lower copy of the color ram is triggered when the raster irq is at half of screen,
; however this would interfere with the sprite raster interrupts. So check the raster line
; during game logic update and do the color ram copy in main cpu thread!

; Preparation of the sprites is done in an IRQ at the bottom of the screen (e.g. start scorepanel).
; Thus it can never be delayed by the screen data transfer when scrolling.
; We need to sort all sprites before we can start display of them.
; The first 8 sprites to display could be programmed at preparation irq or early at start of next
; frame. Next frame would be ideal to also set the scroll hardware registers.
; When further preparation code is an interruptible irq it can be paused by first sprite irq
; and thus it doesn't hurt if preparation takes longer.
; When we set up the first 8 sprites early in the (next) frame we can reprogram them for the score
; panel or even the lower border. However it is not yet planned to use them there,
; so it is easier to let the preparation code set them up.
; The preparation code is then made interruptible, so the irq early in (next) frame can set the first
; sprite mux irq and set the scroll hardware registers. Then it doesn't matter how long the preparation
; takes, whether it finished before start of next frame or takes longer.
; We don't need the irq early in (next) frame for sprite muxing but it is needed to set the VIC control
; registers at the right time: after score panel was drawn and before the next frame begins.
; The sprite mux preparation irq varies greatly in execution time so it can't do that.
; Hence we use an extra irq.

; Note about cli in irq:
; using cli leads to graphical errors, some sprites disappear then.
; Reason is that we don't have acknowledged the irq yet so the VIC
; will trigger a new IRQ just after the cli no matter what rasterline
; is used as trigger, but instantaneously!
; So no cli in IRQ routines except after an acknowledge if the irq should be interceptable by other IRQs.
; so always ack $D019, then cli!

; Note charset blends over sprite data for test from sprite 30 on


; ------------ data for sprite muxer -------------

vsprite_y
!for COUNT, 0, NR_OF_VIRTUAL_SPRITES-1 {
	!byte 0
}
!byte 0	; y value to use as end marker (saves extra check in preparation loop!), set to 255 on init

vsprite_xl
!for COUNT, 0, NR_OF_VIRTUAL_SPRITES-1 {
	!byte 0
}

vsprite_xh
!for COUNT, 0, NR_OF_VIRTUAL_SPRITES-1 {
	!byte 0
}

vsprite_ptr
!for COUNT, 0, NR_OF_VIRTUAL_SPRITES-1 {
	!byte 0	
}

vsprite_col
!for COUNT, 0, NR_OF_VIRTUAL_SPRITES-1 {
	!byte 0
}

bit_nand_table
!for BIT, 0, NR_OF_VIRTUAL_SPRITES-8-1 {
!byte 255-(1<<(BIT&7))
}

bit_or_table
!for BIT, 0, NR_OF_VIRTUAL_SPRITES-8-1 {
!byte (1<<(BIT&7))
}

; generic counter increased by every preparation irq so main program can react
; rarely manipulated/read so don't need to be in zeropage
frame_counter
!byte 0

; Cycles needed for IRQ: 6-9 for IRQ itself, then pha/lda/sta=3+3+4=10
; every block takes: (3+4)*4+3=31 if the bpl is taken.
; This means cycles we need for N sprites:
; N	Cycles
; 1	41
; 2	72
; 3	103
; 4	134
; 5	165
; 6	196
; 7	227
; 8	258
; Worst case is one badline (22 cycles) and several normal lines (63 cycles),
; so we have cycles:
; Lines	Cycles
; 1	22-63
; 2	85-126
; 3	148-189
; 4	211-252
; 5	274-315
; Thus to be sure to display all sprites it takes worst case:
; N	Lines
; 1	1-2, rather 1, but at least 2!
; 2	2
; 3	2-3, rather 2
; 4	3
; 5	3-4, rather 3
; 6	4
; 7	4-5, rather 4
; 8	5
sprite_mux_irq_lines_before_sprite
!byte 2,2,2,3,3,4,4,5

; space needed: NR_VIRT for sort order, NR_V for sprite_sorted_y,
; (NR_V-8)*5 for the other values, so in total NR_V*7-5*8.
; For 32 sprites this would be 184 bytes of zeropage,
; for 24 sprites this would be 128 bytes of zeropage, thats ok.
; use one extra byte in sprite_sort_order as end marker to save one additional stop check in preparation loop!
; This saves 6*16=96 cycles in preparation loop at the cost of only 1 zeropage byte and 1 byte ram, seems good
; Note! The following data doesn't need to be in zeropage, but it is faster that way
slot_raster = 256 - (NR_OF_VIRTUAL_SPRITES-8)
slot_col = slot_raster - (NR_OF_VIRTUAL_SPRITES-8)
slot_ptr = slot_col - (NR_OF_VIRTUAL_SPRITES-8)
slot_xl = slot_ptr - (NR_OF_VIRTUAL_SPRITES-8)
slot_d010 = slot_xl - (NR_OF_VIRTUAL_SPRITES-8)
sprite_sorted_y = slot_d010 - NR_OF_VIRTUAL_SPRITES
; must be in zeropage because of stx xxx,y
sprite_sort_order = sprite_sorted_y - (NR_OF_VIRTUAL_SPRITES+1)
sprite_mux_xhval = sprite_sort_order-1
sprite_mux_sort_index = sprite_sort_order-2	; index in sorted table
sprite_mux_nr_spr_in_irq = sprite_sort_order-3	; uses one byte, nr of sprites in current irq
sprite_mux_current_RL = sprite_sort_order-4	; uses one byte, current rasterline for irq
slot_nr_irq_begin = sprite_sort_order-5
sprite_mux_first_zeropage_address = slot_nr_irq_begin

; Prepare the sprite multiplexer
sprite_mux_init
	; turn on all sprites
	+set8im VICSPRITEENABLE, $FF
	sta vsprite_y+NR_OF_VIRTUAL_SPRITES	; set $FF also as endmarker!
	; prepare any initial sort order - must be in zeropage!
	ldy #NR_OF_VIRTUAL_SPRITES	; one more to also set sort index for stop marker!
-	tya
	sta sprite_sort_order,y
	dey
	bpl -
	; set up irq
	sei
	+set16im $FFFE, sprite_mux_irq_prepare
	+set8im VICRASTER, SPRITE_PREPARE_IRQ_RASTERLINE
	cli
	rts

; *************************************************************
; Routine to prepare next frame's sprite data (IRQ!)
; *************************************************************
; sorts sprite indices by the sprite's y coordinate
; and prepares sprite interrupt data.
; NOTE! Not reentrant code! It uses dedicated zeropage variables without saving them!
;  (fixme) maybe we can put some of these at the end of the arrays, xhval at the end of
; the xh array...
; ************************************************************
sprite_mux_irq_prepare
!if DEBUG_TIMING > 0 { inc VICBGCOL }
!if DEBUG_TIMING > 0 { inc VICBGCOL };fixme
	; save all registers
	pha
	txa
	pha
	tya
	pha
	; prepare two of the three values in x,y registers, then wait for stable
	; raster line with busy waiting, then set the registers and then the third one.
	ldx SCOREPANEL_VICMEMCTRL
	ldy SCOREPANEL_VICCTRL1

	lda #SPRITE_PREPARE_RASTERLINE
-	cmp VICRASTER
	bcs -
!if DEBUG_TIMING > 0 { dec VICBGCOL };fixme
	; switch to another screen where sprite pointers point to empty sprites
	; and char data for line #20 are empty chars.
	; So first set vicmemctrl, then vertical scroll (ctrl1), then horizontal scroll (ctrl2) 
;	stx VICMEMCTRL
;	sty VICCTRL1
;	+copy8 VICCTRL2, SCOREPANEL_VICCTRL2

	; score panel reached, so increase frame counter
	inc frame_counter

	; ------------ now the sprite sorting ---------

	; Changing value for one sprite and sorting it in sorted list would be
	; logN (make heap). Hence all sprites can change position and mostly will
	; every frame, this is N*logN like heap sort. So this will NOT be faster.
	; The fastest continuous sorting algorithm is insertion sort.
	; We also tried bubble sort, which is faster on already sorted arrays
	; but will be very bad in certain yet realistic situations.
	; So use insertion sort here.
	; And just sorting the y values directly is faster, but we need to know
	; which indices are they for, so we would need to sort the index array
	; meanwhile. But since the sorted y values are not final and can't be kept
	; (because of the skip with 9th sprite on a row) we can't do that or need
	; to copy the y values to yet another array - as result it is not faster.
	; So do it with insertion sort of indexed coordinates.

	; as first sort indices so that sprite y coordinates are ascending with
	; insertion sort
	; outer counter of insertion sort loop is stored in y/TEMP0, going from 1 to
	; NR_OF_VIRTUAL_SPRITES-1. Inner counter is y register.
	; The virtual sprite number is in x register.
	; currently processed vsprite index in TEMP1, it's y value in accu
	; the sorting takes many cycles even for already sorted arrays, but we can't
	; remove more commands.
	; Can we speed up this code anyhow? X and Y are needed in inner loop,
	; so we need to use zeropage for storage. Seems this is the minimum
	; version for insertion sort. And insertion sort is the fastest algorithm
	; for nearly sorted arrays.
	; Sorting indices in reverse order to save the cpy call in the outer loop would
	; save at least that call, but that saves only ~3*24 cycles (1 rasterline) total.
	; Even with 32 sprites the sorting is fast enough in realistic situations.
	ldy #1			; outer counter 1...NR_OF_VIRTUAL_SPRITES-1
sprite_mux_sort_outerloop
	ldx sprite_sort_order,y
	lda vsprite_y,x
	sty TEMP0		; save outer counter
	stx TEMP1		; its sort_order[y], is overwritten so save it
sprite_mux_sort_innerloop	; since y>0 always we only check at end of loop
	ldx sprite_sort_order-1,y
	cmp vsprite_y,x		; compare to Y of processed index
	bcs sprite_mux_sort_innerdone
	stx sprite_sort_order,y
	dey
	bne sprite_mux_sort_innerloop
sprite_mux_sort_innerdone
	ldx TEMP1		; current index
	stx sprite_sort_order,y	; store sort_order[y]
	ldy TEMP0 		; load outer counter
	iny
	cpy #NR_OF_VIRTUAL_SPRITES
	bne sprite_mux_sort_outerloop
!if DEBUG_TIMING > 1 { inc VICBGCOL }

; we need to set the hibyte of the screen address for all virtual sprites!
; so one load and 16-32 stores.
sprite_mux_set_ptr
	lda #$07		; hibyte of screen address, reprogrammed from scrolling
sprite_mux_ptr_page = sprite_mux_set_ptr+1
!for COUNT, 0, 7 {
	sta sprite_mux_set_first8+COUNT*FIRST8_SIZE+FIRST8_SCREEN_ADR
}
!for COUNT, 8, NR_OF_VIRTUAL_SPRITES-1 {
	sta sprite_mux_irq_unroll+MUXIRQ_SIZE*(COUNT-8)+MUXIRQ_SCREEN_ADR
}

; next step: skip all sprites with y coords that are too low
; advance y register to first sprite index with visible sprite
; Note that we already start with Y=8 to avoid adding offset 8 later!
; Minus one because of iny before checking.
; Note that if all sprites are too low, this will completely run amok.
; But that case does never happen in practice.
	ldx #8-1
	lda #MIN_SPRITE_Y
-	ldy sprite_sort_order-8+1,x
	inx
	cmp vsprite_y,y
	bcs -

; now up to 8 sprites can be shown directly. Copy their sorted y coordinates
; to the zeropage arrays (we need them there for later lookup).
; We can set the other values directly.
; Do NOT break out when y coordinate is too high!
; Because if it is the sprite must not be used and we need to set some value
; for it so it is not displayed instead of turned off - and that is e.g. a
; high y coordinate!
; Logical consequence is: unroll the loop!
; we need to set the correct screen address later!
	+set8im sprite_mux_xhval, 0	; prepare D010 (xh) register
sprite_mux_set_first8
!for COUNT, 0, 7 {
!zone {
.adrstart
	ldy sprite_sort_order+COUNT-8,x	; -8 because x already with offset 8.
	lda vsprite_y,y
	sta sprite_sorted_y+COUNT
	sta $D001+COUNT*2
	lda vsprite_xl,y
	sta $D000+COUNT*2
	lda vsprite_ptr,y
	sta $07F8+COUNT			; screen address needs replace later
.adrp1
	lda vsprite_col,y
	sta $D027+COUNT
	lda vsprite_xh,y
	beq +
	lda sprite_mux_xhval
	ora #(1 << COUNT)
	sta sprite_mux_xhval
+
.adrend
FIRST8_SCREEN_ADR = .adrp1 - .adrstart - 1
FIRST8_SIZE = .adrend - .adrstart
}
}
	lda sprite_mux_xhval	; must be loaded again because the beq above may have been taken
	sta $D010		; save current xhigh value to register
!if DEBUG_TIMING > 1 { inc VICBGCOL }

; set up irq early at next frame to be triggered. No sei/cli needed here
	+set16im $FFFE, sprite_mux_startframe_irq
	+set8im VICRASTER, STARTFRAME_RASTERLINE
; now allow irqs to interrupt this by acknowledging irq and
; clearing interrupt flag.
	asl $D019
	cli

; copy data for virtual sprites 8-x to irq code!
; up to x-8 iterations:
; load ycoord of next sprite.
; if it is too large, stop irq generation, finish last irq
; compare ycoord to that of 8 sprites before (same slot)
; if ycoord is too close, sprite would be 9th sprite in a rasterline, so skip it
; otherwise sprite can be displayed, but check if in this irq or in new irq
; if new irq, finish current irq and create next (if not first)
; append data to current irq
; while appending irq data update xhvalue
; when all is done, finish last irq
; the slot number is in Y (nr of virtual hardware sprite that is used),
; it runs from 0 to NR_VIRTUAL_SPRITES - 1 and is 8 after setup of the first
; 8 hardware sprites.
	stx sprite_mux_sort_index	; save current index (some virtual sprites could have been skipped!)
	lda #0				; init data for routine
	tay				; slot nr = 0 initially
	sty slot_nr_irq_begin
	sta sprite_mux_nr_spr_in_irq
	sta sprite_mux_current_RL	; first rasterline = 0 as marker, so comparison below is always false for first irq
sprite_mux_prepare_next_vsprite
	ldx sprite_mux_sort_index	; get sort order index
	lda sprite_sort_order,x		; get sort order. 
	tax				; put it in x
	lda vsprite_y,x			; load next virtual sprite y coord
	cmp #MAX_SPRITE_Y		; check that sprite ycoord is visible
	bcs sprite_mux_irqs_prepared	; if too big, stop further displaying
	inc sprite_mux_sort_index	; advance to next sorted index
	sta sprite_sorted_y+8,y		; save sorted y for later use (overwritten if sprite is skipped)
	lda sprite_sorted_y,y		; load sorted y of current slot (8 sprites before!)
	adc #SPRITE_Y_DISTANCE		; C=0 here, also afterwards, compute end of sprite display of that slot
	cmp sprite_sorted_y+8,y		; compare against current sprite y
	bcs sprite_mux_prepare_next_vsprite	; sprite would be 9th in rasterline, skip it!
	; sprite can be displayed - check if in this irq or new irq
	cmp sprite_mux_current_RL	; compare y of last slot + 21 < current rasterline ?
	bcc sprite_mux_prepare_same_irq	; same irq can be used if this slot has finished drawing
	; need new irq
	lda sprite_mux_current_RL	; load current raster line
	beq +				; if 0, this is first irq
	; not first (new) irq, finish last one
	; read the current irq rasterline start and store it for the slot at irq begin
	lda sprite_mux_current_RL
	ldx slot_nr_irq_begin
	sta slot_raster,x
	; prepare xhigh value
	lda sprite_mux_xhval
	sta slot_d010,x			; store at matching slot
	sty slot_nr_irq_begin		; memorize for next irq
	+set8im sprite_mux_nr_spr_in_irq, 0	; 0 sprites in this irq
	; set highest bit of last written color to flag end of irq!
	lda slot_col-1,y		; write to slot before, so -1 here
	ora #$80
	sta slot_col-1,y
+
	lda #$FF			; real value is set later, use neutral value for mininum computation.
	sta sprite_mux_current_RL	; save as rasterline
sprite_mux_prepare_same_irq
	ldx sprite_mux_sort_index	; set x again to sort order
	lda sprite_sort_order-1,x	; -1 because y alreadly inc'd
	tax
	lda vsprite_xl,x
	sta slot_xl,y
	lda vsprite_ptr,x
	sta slot_ptr,y
	lda vsprite_col,x
	sta slot_col,y
	lda sprite_mux_xhval		; xhigh
	and bit_nand_table,y		; slotnumber (NUM_VSPRITES-8 sized!)
	sta sprite_mux_xhval
	lda vsprite_xh,x
	beq +
	lda sprite_mux_xhval
	ora bit_or_table,y
	sta sprite_mux_xhval
+	
	; load sprite y and decrease by offset depending on number of sprites in interrupt
	lda vsprite_y,x
	ldx sprite_mux_nr_spr_in_irq
	sec
	sbc sprite_mux_irq_lines_before_sprite,x
	; compare if that is lower than current irq raster start, if so, use it
	cmp sprite_mux_current_RL
	bcs +
	sta sprite_mux_current_RL
+
	inc sprite_mux_nr_spr_in_irq	; sprite handled
	iny				; use next slot
!if DEBUG_TIMING > 1 { inc VICBGCOL }
	jmp sprite_mux_prepare_next_vsprite

sprite_mux_irqs_prepared
!if DEBUG_TIMING > 1 { inc VICBGCOL }
	; store the irq start raster line for that slot
	lda sprite_mux_current_RL
	ldx slot_nr_irq_begin
	sta slot_raster,x				; to slot where irq begun
	; set xhigh value of last irq
	lda sprite_mux_xhval
	sta slot_d010,x
	; if all virtual sprites are handled but not all slots used because some
	; sprites are skipped because of 9th sprite in a row or too small y coordinates
	; we have to signal the end of all irqs by setting 7th bit of color of last slot
	; and setting the next raster line to 0 as marker.
	; In that case y has not reached maximum value
	cpy #(NR_OF_VIRTUAL_SPRITES-8)
	beq +
	lda slot_col-1,y		; write to slot before, so -1 here
	ora #$80
	sta slot_col-1,y
	lda #0
	sta slot_raster,y		; end marker
+	; if only 8 sprites are used, sprite_mux_current_RL is 0.
	; so we can request that value in the startframe irq as marker!
	; end this (already interruptible) irq.
	pla
	tay
	pla
	tax
!if DEBUG_TIMING > 1 { +set8im VICBGCOL, 14 }
!if DEBUG_TIMING = 1 { dec VICBGCOL }
	pla
	rti

sprite_mux_startframe_irq
!if DEBUG_TIMING > 0 { inc VICBGCOL }
	pha
	lda sprite_mux_current_RL	; 0 when only 8 sprites drawn
	bne +
	; set irq so that next frame is prepared
	+set16im $FFFE, sprite_mux_irq_prepare
	+set8im VICRASTER, SPRITE_PREPARE_IRQ_RASTERLINE
	bne ++	; highbyte is !0, so always jumps
+	; prepare first mux irq
	+copy8 VICRASTER, slot_raster
	+set16im $FFFE, sprite_mux_irq
	; set end marker for last irq (high bit of color) is not needed
++
	; now prepare hardware registers when scrolling is active
!ifndef ONLY_MUXER {
	+copy8 VICCTRL1, scroll_vicctrl1
	+copy8 VICCTRL2, scroll_vicctrl2
	+copy8 VICMEMCTRL, scroll_vicmemctrl
}
	pla
	asl $D019
!if DEBUG_TIMING > 0 { dec VICBGCOL }
	rti

; ******************************************
; the magic sprite mux irq
; ******************************************
MIN_RASTER_LINES_BETWEEN_IRQS = 3	; with 2 we miss rasterlines/irqs and many sprites are missing
sprite_mux_irq

!if UNROLL_FULL_IRQ {


!if DEBUG_TIMING > 0 { inc VICBGCOL }
	pha				; save accu
	lda slot_d010			; load xhigh for irq start at that slot
	sta $D010			; set xhigh for this irq
sprite_mux_irq_unroll
	; unroll one iteration less than needed, last iteration is shorter and manually unrolled.
	; note that the instructions must be identical in the last iteration to make it work!
!for COUNT, 0, NR_OF_VIRTUAL_SPRITES-2-8 {
!zone {
.adrstart
	lda sprite_sorted_y + COUNT + 8	; load y coord
	sta $D001+(COUNT & 7)*2		; set for hardware sprite
	lda slot_xl + COUNT		; load xl coord
	sta $D000+(COUNT & 7)*2		; set for hardware sprite
	lda slot_ptr + COUNT		; load sprite pointer
	sta $07F8+(COUNT & 7)		; set in register
.adrp1
	lda slot_col + COUNT		; load color
	sta $D027+(COUNT & 7)		; set in register
	bpl .adrend			; if highbit set irq ends, so highbit clear, do next slot
	lda slot_raster + COUNT+1	; load next irq raster line, next so +1
	; when accu is 0 so zero bit is set, no new irq needs to be called
	; but the last one needs to end!
	bne +
	jmp sprite_mux_irq_last_done
+	sta VICRASTER			; set as irq trigger
	cld
	sec				; check if we are late for next irq!
	sbc #MIN_RASTER_LINES_BETWEEN_IRQS
	cmp VICRASTER
	; if C=0 we are too late for next irq, but do not start it BEFORE the
	; stored raster line or it will be too early!
	; if we are too late for the irq all displaying fails from here on
	bcc +				; if carry set, VICRASTER <= next_rasterline - 2, so end IRQ
	+set16im $FFFE, .nextirqstart
	pla		; restore accu
	asl $D019	; ack irq
!if DEBUG_TIMING > 0 { dec VICBGCOL }
	rti
+	adc #(MIN_RASTER_LINES_BETWEEN_IRQS-1)	; C=0, prepare check VICRASTER <= next_rasterline - 1
!if DEBUG_TIMING > 1 { inc VICBGCOL }
-	cmp VICRASTER			; as long as we are in line before, do busy waiting
	bcs -
!if DEBUG_TIMING > 1 { dec VICBGCOL }
	bcc +				; directly jump to next irq code (always branches)
.nextirqstart
!if DEBUG_TIMING > 0 { inc VICBGCOL }
	pha				; save accu
+	lda slot_d010 + COUNT+1		; load xhigh for irq start at that slot
	sta $D010			; set xhigh for this irq
.adrend
MUXIRQ_SIZE = .adrend - .adrstart
MUXIRQ_SCREEN_ADR = .adrp1 - 1 - .adrstart
} ; zone
} ; for
; Now iteration for COUNT=NR_OF_VIRTUAL_SPRITES-1-8 manually unrolled, only
; a few instructions. Must be identical to unrolled irq code!
!zone {
.COUNT=NR_OF_VIRTUAL_SPRITES-1-8
	lda sprite_sorted_y + .COUNT + 8	; load y coord
	sta $D001+(.COUNT & 7)*2		; set for hardware sprite
	lda slot_xl + .COUNT		; load xl coord
	sta $D000+(.COUNT & 7)*2		; set for hardware sprite
	lda slot_ptr + .COUNT		; load sprite pointer
	sta $07F8+(.COUNT & 7)		; set in register
	lda slot_col + .COUNT		; load color
	sta $D027+(.COUNT & 7)		; set in register
}
sprite_mux_irq_last_done
	lda #SPRITE_PREPARE_IRQ_RASTERLINE-3	; need some lines in advance
	cmp VICRASTER
	bcs +
	; we are too late for preparation irq, call that directy
	pla
!if DEBUG_TIMING > 0 { dec VICBGCOL }
	jmp sprite_mux_irq_prepare
+	; last irq is done, trigger preparation irq then
	+set16im $FFFE, sprite_mux_irq_prepare
	+set8im VICRASTER, SPRITE_PREPARE_IRQ_RASTERLINE
	pla
!if DEBUG_TIMING > 0 { dec VICBGCOL }
	; ack and end irq
	asl $D019
	rti
sprite_mux_irq_code_end



} else {	; UNROLL_FULL_IRQ
	; here the partially unrolled version

!if DEBUG_TIMING > 0 { inc VICBGCOL }
	pha				; save accu
	lda slot_d010			; load xhigh for irq start at that slot
	sta $D010			; set xhigh for this irq
sprite_mux_irq_unroll
	; unroll one iteration less than needed, last iteration is shorter and manually unrolled.
	; note that the instructions must be identical in the last iteration to make it work!
!for COUNT, 0, NR_OF_VIRTUAL_SPRITES-2-8 {
!zone {
.adrstart
	lda sprite_sorted_y + COUNT + 8	; load y coord
	sta $D001+(COUNT & 7)*2		; set for hardware sprite
	lda slot_xl + COUNT		; load xl coord
	sta $D000+(COUNT & 7)*2		; set for hardware sprite
	lda slot_ptr + COUNT		; load sprite pointer
	sta $07F8+(COUNT & 7)		; set in register
.adrp1
	lda slot_col + COUNT		; load color
	sta $D027+(COUNT & 7)		; set in register
	bpl .adrend			; if highbit set irq ends, so highbit clear, do next slot
	lda slot_raster + COUNT+1	; load next irq raster line, next so +1
	jsr sprite_mux_irq_prepare_next	; save address for next irq or returns if late
	bcc +				; carry is clear from caller, so this always branches
!if DEBUG_TIMING > 0 { inc VICBGCOL }
	pha				; save accu
+	lda slot_d010 + COUNT+1		; load xhigh for irq start at that slot
	sta $D010			; set xhigh for this irq
.adrend
MUXIRQ_SIZE = .adrend - .adrstart
MUXIRQ_SCREEN_ADR = .adrp1 - 1 - .adrstart
} ; zone
} ; for
; Now iteration for COUNT=NR_OF_VIRTUAL_SPRITES-1-8 manually unrolled, only
; a few instructions. Must be identical to unrolled irq code!
!zone {
.COUNT=NR_OF_VIRTUAL_SPRITES-1-8
	lda sprite_sorted_y + .COUNT + 8	; load y coord
	sta $D001+(.COUNT & 7)*2		; set for hardware sprite
	lda slot_xl + .COUNT		; load xl coord
	sta $D000+(.COUNT & 7)*2		; set for hardware sprite
	lda slot_ptr + .COUNT		; load sprite pointer
	sta $07F8+(.COUNT & 7)		; set in register
	lda slot_col + .COUNT		; load color
	sta $D027+(.COUNT & 7)		; set in register
}
sprite_mux_irq_last_done
	; last irq is done, trigger preparation irq then
	lda #SPRITE_PREPARE_IRQ_RASTERLINE-3	; need some lines in advance
	cmp VICRASTER
	bcs +
	; we are too late for preparation irq, call that directy
	pla
!if DEBUG_TIMING > 0 { dec VICBGCOL }
	jmp sprite_mux_irq_prepare
+	; last irq is done, trigger preparation irq then
	+set16im $FFFE, sprite_mux_irq_prepare
	+set8im VICRASTER, SPRITE_PREPARE_IRQ_RASTERLINE
	pla
!if DEBUG_TIMING > 0 { dec VICBGCOL }
	; ack and end irq
	asl $D019
	rti

sprite_mux_irq_prepare_next
	; when accu is 0 so zero bit is set, no new irq needs to be called
	; but the last one needs to end!
	bne +
	pla				; undo jsr
	pla				; undo jsr
	jmp sprite_mux_irq_last_done
+	sta VICRASTER			; set as irq trigger
	cld				; needed, as the main program can use decimal mode anywhere
	sec				; check if we are late for next irq!
	sbc #MIN_RASTER_LINES_BETWEEN_IRQS
	cmp VICRASTER
	; if C=0 we are too late for next irq, but do not start it BEFORE the
	; stored raster line or it will be too early!
	; if we are too late for the irq all displaying fails from here on
	bcs sprite_mux_irq_done		; if carry set, VICRASTER <= next_rasterline - 2, so end IRQ
	adc #(MIN_RASTER_LINES_BETWEEN_IRQS-1)	; C=0, prepare check VICRASTER <= next_rasterline - 1
!if DEBUG_TIMING > 1 { inc VICBGCOL }
-	cmp VICRASTER			; as long as we are in line before, do busy waiting
	bcs -
!if DEBUG_TIMING > 1 { dec VICBGCOL }
	rts				; return to unrolled code to continue irq.
sprite_mux_irq_done			; set up next irq vector from saved address and end irq
	pla
	clc
	adc #3
	sta $FFFE	; need to add 1 to address, because of the way JSR works, +2 to skip the bcc.
	pla
	adc #0
	sta $FFFF
	pla		; restore accu
	asl $D019	; ack irq
!if DEBUG_TIMING > 0 { dec VICBGCOL }
	rti
sprite_mux_irq_code_end

} ; if UNROLL_FULL_IRQ

